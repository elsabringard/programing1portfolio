class Laser {
  int x, y, w, h, speed;
  color c1;

  // Constructor
  Laser(int x, int y) {
    this.x = x;
    this.y = y;
    w=4;
    h = 10;
    speed = 10;
    c1 = color(#00FA1F);
  }

  void display() {
    //x = tempX;
    //y = tempY;
    rectMode(CENTER);
    noStroke();
    fill(c1);
    rect(x, y, w, h);
  }

  void move() {
    y -= speed;
  }

  boolean reachedTop() {
    if (y<-10) {
      return true;
    } else {
      return false;
    }
  }

  boolean intersect(Laser laser) {
    float d = dist(x, y, laser.x, laser.y);
    if (d<laser.w/2) {
      return true;
    } else {
      return false;
    }
  }
}

class PowerUp {
  int x, y, speed, diam;
  char type;

  // Constructor
  PowerUp() {
    x = int(random(width));
    y = -50;
    speed = int(random(2, 10));
    diam = 50;
    if (int(random(2))== 0) {
      type= 'H';
    } else {
      type = 'A';
    }
  }

  void display() {
    fill(#9834FF);
    ellipse(x, y, diam, diam);
    fill(222);
    text(type,x,y);
  }

  void move() {
    y += speed;
  }

  boolean reachedBottom() {
    if (y>height+100) {
      return true;
    } else {
      return false;
    }
  }
    boolean intersect(Ship ship) {
    float d = dist(x, y, ship.x, ship.y);
    if (d<30) {
      return true;
    } else {
      return false;
    }
  }
}

class Rock {
  int x, y, speed, diam;
  PImage rock;

  // Constructor
  Rock() {
    x = int(random(width));
    y = -50;
    speed = int(random(2, 10));
    diam = 50;
    rock = loadImage("rock.png");
  }

  void display() {
    rock.resize(150, 150);
    imageMode(CENTER);
    image(rock, x, y);
  }

  void move() {
    y += speed;
  }

  boolean reachedBottom() {
    if (y>height+100) {
      return true;
    } else {
      return false;
    }
  }
  boolean intersect(Laser laser) {
    float d = dist(x, y, laser.x, laser.y);
    if (d<30) {
      return true;
    } else {
      return false;
    }
  }
}

class Ship {
  int x, y, w, ammo, turretCount, health;
  PImage ship;

  Ship() {
    x = 0;
    y = 0;
    w = 98;
    ammo = 1000;
    turretCount = 3;
    health= 1000;
    ship = loadImage("Ship.png");
  }

  void display(int tempx, int tempy) {
    x = tempx;
    y = tempy;
    image(ship, x, y);
    ship.resize(98, 98);
    imageMode(CENTER);
  }

  void move () {
  }

  boolean fire() {
    if (ammo>0) {
      ammo--;
      return true;
    } else {
      return false;
    }
  }

  boolean intersect(Rock rock) {
    float d = dist(x, y, rock.x, rock.y);
    if (d<rock.diam/2) {
      return true;
    } else {
      return false;
    }
  }
}

// Elsa Bringard | november 28 | Space game
import processing.sound.*;
SoundFile blaster;
Ship s1;
Timer rockTimer, puTimer;
ArrayList<Rock> rocks = new ArrayList<Rock>();
ArrayList<PowerUp> powerups = new ArrayList <PowerUp>();
ArrayList<Laser> lasers = new ArrayList<Laser>();
Star [] stars = new Star[1000];
int score, level, rockRate, Rocks, Lasers, ammo, rocksPassed;
boolean play;

void setup() {
  size (800, 800);
  s1 = new Ship();
  rockRate = 500;
  puTimer = new Timer(5000);
  puTimer.start();
  rockTimer = new Timer(rockRate);
  rockTimer.start();
  blaster = new SoundFile(this, "blast.wav");
  for (int i=0; i<stars.length; i++) {
    stars [i] = new Star ();
  }
  score = 0;
  level = 1;
  //Laser Count = 1000;
  play = false;
  rocksPassed=0;
}

void draw() {
  if (!play) {
    startScreen();
  } else {
    
    //level handling
    if (frameCount % 1000 ==10) {
      level++;
      rockRate-=10;
    }
    
    background(1);

    //rendering stars
    for (int i=0; i<stars.length; i++) {
      stars [i].display ();
      stars [i].move ();
    }
    noCursor();
    

    // Add PowerUps
    if (puTimer.isFinished()) {
      powerups.add(new PowerUp());
      puTimer.start();
      println("PowerUps:" + powerups.size());
    }

    // Rendering PowerUps and Detectiong ship collision
    for (int i = 0; i < powerups.size(); i++) {
      PowerUp pu = powerups.get(i);
      if (pu.intersect(s1)) {
        if (pu.type == 'H') {
          s1.health+=100;
        } else {
          s1.ammo+=100;
        }
        powerups.remove(pu);
      }
      if (pu.reachedBottom()) {
        powerups.remove(pu);
        Rocks ++;
      } else {
        pu.display();
        pu.move();
      }
    }

    // Add Rocks
    if (rockTimer.isFinished()) {
      rocks.add(new Rock());
      rockTimer.start();
      println("Rocks:" + rocks.size());
    }

    // Rendering rocks and Detectiong ship collision
    for (int i = 0; i < rocks.size(); i++) {
      Rock r = rocks.get(i);
      if (s1.intersect(r)) {
        s1.health-=r.diam;
        // todo call the explosion animation
        //add a sound for explosion
        // todo: consider adding rock health
        score -= 25;
        rocksPassed++;
        rocks.remove(r);
      }
      if (r.reachedBottom()) {
        rocks.remove(r);
        Rocks ++;
      } else {
        r.display();
        r.move();
      }
    }

    //Render lasers on the screen and detect rock collision
    for (int i = 0; i < lasers.size(); i++) {
      Laser l = lasers.get(i);
      for (int j = 0; j < rocks.size(); j++) {
        Rock r = rocks.get(j);
        if (r.intersect(l)) {
          score+=50;
          //Todo: add sound to collision
          //Todo: Add animation to the collision
          //todo: dectrament to rock health
          lasers.remove(l);
          rocks.remove(r);
        }
        if (l.reachedTop()) {
          Lasers ++;
          lasers.remove(l);
        } else {
          l.display();
          l.move();
        }
      }
    }

    s1.display(mouseX, mouseY);
    infoPanel();

    // Game over logic
    if (s1.health<1 || Rocks > 10) {
      gameOver();
      noLoop();
    }
  }
}

//Add laser based on event
void mousePressed() {
  blaster.stop();
  blaster.play();
  if (s1.fire() && s1.turretCount == 1) {
    lasers.add (new Laser(s1.x, s1.y));
    println("Lasers:" + lasers.size());
  } else  if (s1.fire() && s1.turretCount == 2) {
    lasers.add (new Laser(s1.x - 20, s1.y));
    lasers.add (new Laser(s1.x + 20, s1.y));
    println("Lasers:" + lasers.size());
  } else  if (s1.fire() && s1.turretCount == 3) {
    lasers.add (new Laser(s1.x + 20, s1.y));
    lasers.add (new Laser(s1.x - 20, s1.y));
    lasers.add (new Laser(s1.x + 0, s1.y));
    println("Lasers:" + lasers.size());
  }
}

void keyPressed() {
  blaster.stop();
  blaster.play();
  if (key == ' ') {
    if (s1.fire() && s1.turretCount == 1) {
      lasers.add (new Laser(s1.x, s1.y));
      println("Lasers:" + lasers.size());
    } else  if (s1.fire() && s1.turretCount == 2) {
      lasers.add (new Laser(s1.x - 20, s1.y));
      lasers.add (new Laser(s1.x + 20, s1.y));
      println("Lasers:" + lasers.size());
    } else  if (s1.fire() && s1.turretCount == 3) {
      lasers.add (new Laser(s1.x + 20, s1.y));
      lasers.add (new Laser(s1.x - 20, s1.y));
      lasers.add (new Laser(s1.x +0, s1.y));
      println("Lasers:" + lasers.size());
    }
  }
}


void infoPanel() {
  fill(129, 128);
  rectMode(CENTER);
  rect(width/2, 25, width, 50);
  fill(255);
  textSize(23);
  text("Score:" + score, 60, 40);
  text("Level:" + level, 200, 40);
  text("Rocks Passed:" + Rocks, 350, 40);
  text("Laser Count:" + Lasers, 530, 40);
  text("Health:" + s1.health, 690, 40);
  //text("Rocks Passed" + rocksPassed, 700, 40);
}

void startScreen() {
  background(0);
  fill(222);
  textAlign(CENTER);
  text("Press any key to begin...", width/2, height/2);
  if (mousePressed || keyPressed) {
    play = true;
  }
}

void gameOver() {
  background(0);
  fill(222);
  fill(#0F9018);
  textAlign(CENTER);
  text("OH NO you dead! Game over!!!", width/2, height/2);
  play = false;
  noLoop();
}

class Star {
  int x, y, speed, diam;

  // Constructor
  Star() {
    x = int(random(width));
    y = int(random(height));
    speed = int(random(2, 6));
    diam = int(random(1, 4));
  }

  void display() {
    fill(random(255, 225));
    ellipse(x, y, diam, diam);
  }

  void move() {
    if (y>height+5) {
      y = -5;
    } else {
      y += speed;
    }
  }

  boolean reachedBottom() {
    return true;
  }
}

// Daniel shiffman Raincatcher game// Example 10-5: Object-oriented timer

class Timer {

  int savedTime; // When Timer started
  int totalTime; // How long Timer should last

  Timer(int tempTotalTime) {
    totalTime = tempTotalTime;
  }

  // Starting the timer
  void start() {
    // When the timer starts it stores the current time in milliseconds.
    savedTime = millis();
  }

  // The function isFinished() returns true if 5,000 ms have passed.
  // The work of the timer is farmed out to this method.
  boolean isFinished() {
    // Check how much time has passed
    int passedTime = millis()- savedTime;
    if (passedTime > totalTime) {
      return true;
    } else {
      return false;
    }
  }
}
